namespace com.ti.oss.common.cdr.sqm ;

use com.ibm.streamsx.file::* ;
use com.ti.oss.common.cdr.parser.eggsn::* ;
use com.ti.oss.common.cdr.types::* ;
use com.ti.oss.common.cdr.util::* ;
use com.ti.oss.common.cdr.util.cfg::* ;
use com.ti.oss.common.event::* ;
use com.ti.oss.common.types::* ;
use com.ti.oss.common.util.cfg::* ;
use com.ti.oss.common.util.time::*;

type SqmBroadImsiCapnet = map<rstring, BroadImsiCapnet>;
type SqmBroadImsiCapnetNode = map<rstring, SqmBroadImsiCapnet>;

public composite BroadBandImsiCapnet(input stream<AggregateSqmSchema> eggsn; 
	output stream<BroadImsiCapnet> outKpiImsiCapnet, OutEventKpiImsiCapnet, stream<CDRCounters> OutCounters)
{
	param
		expression<rstring> $tmpOutFilePrefix ;
		expression<rstring> $tmpOutFileSuffix ;
		expression<rstring> $mediationName ;
	
	graph
		(stream<BroadImsiCapnet> outKpiImsiCapnet as O ; stream<Event e> OutEventKpiImsiCapnet as OE; stream<CDRCounters> OutCounters) = Custom(eggsn)
		{
			logic
				state :
				{
					rstring tmpOutFilePrefix = $tmpOutFilePrefix;
					rstring tmpOutFileSuffix = $tmpOutFileSuffix;
					rstring mediationName = $mediationName;
					
					mutable float64 startROPstate = 0 ;
					mutable float64 endROPstate = 0 ;
					mutable float64 startROPold = 0 ;
					mutable float64 endROPold = 0 ;
					boolean flagMediationCfg = loadMediationCfg(mediationName) ;
					mutable rstring nameOutputFilestate ;
					mutable rstring nameOutputFileOld ;
					mutable rstring path = getCfgParam("COMMON_CFG_DIR",mediationName,"SQM_IMSI_CAPNET_OUTPUT_PATH");
					mutable rstring filePrefix = getCfgParam("COMMON_CFG_DIR",mediationName,"FILE_PREFIX_IMSI_CAPNET");
					mutable rstring fileSuffix = getCfgParam("COMMON_CFG_DIR",mediationName,"FILE_SUFFIX");
					mutable int32 interval = (int32)getCfgParam("COMMON_CFG_DIR",mediationName,"ROP_INTERVAL_SQM");
					mutable SqmBroadImsiCapnet CDRforBroadImsi ;
					mutable SqmBroadImsiCapnetNode SqmBroadImsi ;
					mutable SqmBroadImsiCapnetNode SqmBroadImsiNodePrec ;
					mutable FileHeader fh = { } ;
					
					mutable O emptyEndRopTuple = initEmptyEndRopTupleImsiCapnet();
					mutable OE oetuple = (OE){};
					
					mutable SqmBroadImsiCapnet cdrApp = {};
					
//					// Conta tutti CDR per rATType
//					mutable uint64 cdrtotrattype = 0ul;
//					mutable uint64 cdrtotrattypeold = 0ul;
//					
					// Conta i CDR corretti per questo KPI
					mutable uint64 cdrtot = 0ul;
					mutable uint64 cdrtotold = 0ul;
					
					// Conta i CDR scartati per il filtro impostato
					mutable uint64 cdrexclude1 = 0ul;
					mutable uint64 cdrexclude1old = 0ul;
					
					// Conta i CDR scartati esterni al ROP
					mutable uint64 cdrexclude2 = 0ul;
					mutable uint64 cdrexclude2old = 0ul;
				}

				onTuple eggsn :
				{
					if (eggsn.endROP)
					{
						interval = checkConfiguration(mediationName,"ROP_INTERVAL_SQM",interval);
						path = checkConfiguration(mediationName,"SQM_IMSI_CAPNET_OUTPUT_PATH",path);
						filePrefix = checkConfiguration(mediationName,"FILE_PREFIX_IMSI_CAPNET",filePrefix);
						fileSuffix = checkConfiguration(mediationName,"FILE_SUFFIX",fileSuffix);
				
						swap(SqmBroadImsi,SqmBroadImsiNodePrec);
						swap(cdrtot, cdrtotold);
						cdrtot = 0ul;
						swap(cdrexclude1, cdrexclude1old);
						cdrexclude1 = 0ul;
						swap(cdrexclude2, cdrexclude2old);
						cdrexclude2 = 0ul;
//						swap(cdrtotrattype, cdrtotrattypeold);
//						cdrtotrattype = 0ul;
//						
						clearM(SqmBroadImsi);
						startROPold = startROPstate;
						endROPold = endROPstate;
						startROPstate = eggsn.startRopSecs;
						endROPstate = eggsn.endRopSecs;
						nameOutputFileOld = nameOutputFilestate;
					}
					
					if(startROPstate == (float64)0 || endROPstate == (float64)0)
					{
						startROPstate = eggsn.startRopSecs;
						endROPstate = eggsn.endRopSecs;
					}
					
					if(nameOutputFilestate == "")
					{
						nameOutputFilestate = path + tmpOutFilePrefix +(rstring) eggsn.endRopSecs + tmpOutFileSuffix ;
					}
					
					if(eggsn.endROP)
					{
						nameOutputFilestate = path + tmpOutFilePrefix +(rstring) eggsn.endRopSecs + tmpOutFileSuffix ;
					}					
					
					if (eggsn.write_prev)
					{	
						if (eggsn.exclude_node == "")
						{
							mutable rstring filename_tmp = "";
							mutable rstring central_val="";
							
							if(size(SqmBroadImsiNodePrec) != 0)
							{
								mutable list<rstring> keysTmp = [];
                   				for(rstring key in SqmBroadImsiNodePrec) 
								{
									appendM(keysTmp, key);
								}
								for(rstring key in keysTmp)
								{
									swap(cdrApp, SqmBroadImsiNodePrec[key]);
									for(rstring keyCdr in cdrApp) 
									{	
										submit(cdrApp[keyCdr], outKpiImsiCapnet);
				                    }
				                    central_val = central_val + key + ";";	
								}
								
								filename_tmp = nameOutputFileOld;
								emptyEndRopTuple.nameOutputFile = nameOutputFileOld;
								submit(emptyEndRopTuple, outKpiImsiCapnet);									
							}
							else
							{
								filename_tmp = nameOutputFileOld;
								emptyEndRopTuple.nameOutputFile = nameOutputFileOld;
								submit(emptyEndRopTuple, outKpiImsiCapnet);
							}
							
							mutable rstring sr = "";
							mutable rstring er = "";
							if(startROPold != (float64)0 && endROPold != (float64)0)
							{
								sr = getUtilConvert((rstring)startROPold);
								er = getUtilConvert((rstring)endROPold);
							}
							else
							{
								sr = "0";
								er = "0";
							}
							submit({kpi = "BroadBandImsiCapnet", ROP = sr + "-" + er, cdrtot = cdrtotold, cdrexclude1 = cdrexclude1old, cdrexclude2 = cdrexclude2old}, OutCounters);
							
							//clear statistics
							//cdrtotrattypeold = 0ul;
							cdrtotold = 0ul;
							cdrexclude1old = 0ul;
							cdrexclude2old = 0ul;
							
							clearM(SqmBroadImsiNodePrec);
							clearM(cdrApp);
							startROPold = (float64)0;
							endROPold = (float64)0;
							nameOutputFileOld = "";
							oetuple.e.eventType = 190 ;
							oetuple.e.resultCode = 0 ;
							oetuple.e.rawFile = filename_tmp ;
							swap(oetuple.e.eventCorrelationId, fh.eventCorrelationId);
		                    swap(oetuple.e.rop, fh.rop);
		                    swap(oetuple.e.emId, fh.emId);
		                    swap(oetuple.e.neId, fh.neId);
							oetuple.e.eventText = "End write_prev close file " + filename_tmp + " for node: "+ central_val;
							boolean eventEnabled = mkEvent(oetuple.e, fh.sessionBeginTime) ;
							if(eventEnabled) submit(oetuple, OutEventKpiImsiCapnet) ;
						}
						else
						{
							if(has(SqmBroadImsiNodePrec, eggsn.exclude_node) == true)
							{
								removeM(SqmBroadImsiNodePrec, eggsn.exclude_node) ;
							}
						}
					}
					else if(!eggsn.endROP)
					{
						fh = eggsn.fHeader;
						
						//cdrtotrattype++;							
						mutable rstring datetmp = "20"+(rstring)substring(eggsn.cdrSchema.recordOpeningTime,0,12);
			            //appTrc(Trace.error,"datetmp: "+datetmp);
			            timestamp ts = toTimestamp(Sys.YYYYMMDDhhmmss,datetmp);
			            //appTrc(Trace.error,"ts: "+(rstring)ts);
			            mutable float64 endTimeCDRfloat = getSeconds(ts) + (int64)eggsn.cdrSchema.duration;
						
						mutable rstring centralName = eggsn.cdrSchema.nodeId; 
						mutable rstring key = eggsn.cdrSchema.servedMSISDN;
						
						/*appTrc(Trace.error,"endTimeCDRfloat: "+(rstring)endTimeCDRfloat);
						appTrc(Trace.error,"eggsn.startRopSecsOld: "+(rstring)eggsn.startRopSecsOld);
						appTrc(Trace.error,"eggsn.endRopSecsOld: "+(rstring)eggsn.endRopSecsOld);
						appTrc(Trace.error,"eggsn.startRopSecs "+(rstring)eggsn.startRopSecs);
						appTrc(Trace.error,"eggsn.endRopSecs "+(rstring)eggsn.endRopSecs);*/
						
						if((endTimeCDRfloat >= eggsn.startRopSecsOld) && (endTimeCDRfloat < eggsn.endRopSecsOld))
                		{
                			cdrtotold++;
	                		if(has(SqmBroadImsiNodePrec, centralName) == true)
	                		{
	                			addCdrImsi(eggsn,SqmBroadImsiNodePrec[centralName],key,nameOutputFileOld,filePrefix,fileSuffix,eggsn.startRopSecsOld,eggsn.endRopSecsOld);	
	                		}
	                		else
	                		{
	                			addCdrImsi(eggsn,CDRforBroadImsi,key,nameOutputFileOld,filePrefix,fileSuffix,eggsn.startRopSecsOld,eggsn.endRopSecsOld);
	                			insertM(SqmBroadImsiNodePrec, centralName, CDRforBroadImsi) ;
	                		}
	                		
                		}
                		else if((endTimeCDRfloat >= eggsn.startRopSecs) && (endTimeCDRfloat < eggsn.endRopSecs))
                		{
                			cdrtot++;
                			if(has(SqmBroadImsi, centralName) == true)
		                	{
		                		addCdrImsi(eggsn,SqmBroadImsi[centralName],key,nameOutputFilestate,filePrefix,fileSuffix,eggsn.startRopSecs,eggsn.endRopSecs);
		                	}
		                	else
		                	{
		                		addCdrImsi(eggsn,CDRforBroadImsi,key,nameOutputFilestate,filePrefix,fileSuffix,eggsn.startRopSecs,eggsn.endRopSecs);
								insertM(SqmBroadImsi, centralName, CDRforBroadImsi) ;
		                	}
                		}
                		else
						{
							//exclude CDR fuori ROP
							cdrexclude2++;
						}
						
						clearM(cdrApp);
						clearM(CDRforBroadImsi);
					}
			}
								
			onPunct eggsn :
			{
				if(currentPunct() == Sys.WindowMarker)
				{
					
					oetuple.e.eventType = 200 ;
					oetuple.e.resultCode = 0 ;
					oetuple.e.rawFile = filename(fh.fName) ;
					swap(oetuple.e.eventCorrelationId, fh.eventCorrelationId);
                    swap(oetuple.e.rop, fh.rop);
                    swap(oetuple.e.emId, fh.emId);
                    swap(oetuple.e.neId, fh.neId);
					oetuple.e.eventText = "End Broadband Imsi Capnet SQM";
					boolean eventEnabled = mkEvent(oetuple.e, fh.sessionBeginTime) ;
					if(eventEnabled) submit(oetuple, OutEventKpiImsiCapnet) ;
					
					submit(Sys.WindowMarker, OutEventKpiImsiCapnet) ;
				}

			}
			
			config
				threadedPort: queue(eggsn, Sys.Wait, 1000000);

		}
	}


void countCdrsIMSI(AggregateSqmSchema eggsn1, mutable BroadImsiCapnet aggrSQMtmp) 
{
	
	if (eggsn1.cdrSchema.rATType == 6)
	{	
		for(int32 i in range(eggsn1.cdrSchema.datavolumeFBCDownlink))
		{
			if (eggsn1.cdrSchema.datavolumeFBCDownlink[i] != "0")
			{
				//appTrc(Trace.error,"incremento imsi_lte_dl_somma_volume");
				aggrSQMtmp.imsi_lte_dl_somma_volume = aggrSQMtmp.imsi_lte_dl_somma_volume + (int64)eggsn1.cdrSchema.datavolumeFBCDownlink[i];
			}
		}
		for(int32 i in range(eggsn1.cdrSchema.datavolumeFBCUplink))
		{
			if (eggsn1.cdrSchema.datavolumeFBCUplink[i] != "0")
			{
				//appTrc(Trace.error,"incremento imsi_lte_ul_somma_volume");
				aggrSQMtmp.imsi_lte_ul_somma_volume = aggrSQMtmp.imsi_lte_ul_somma_volume + (int64)eggsn1.cdrSchema.datavolumeFBCUplink[i];
			}
		}
	}
	else if (eggsn1.cdrSchema.rATType == 1 || eggsn1.cdrSchema.rATType == 2)
	{			
		for(int32 i in range(eggsn1.cdrSchema.datavolumeFBCDownlink))
		{
			if (eggsn1.cdrSchema.datavolumeFBCDownlink[i] != "0")
			{
				//appTrc(Trace.error,"incremento imsi_bb_dl_somma_volume");
				aggrSQMtmp.imsi_bb_dl_somma_volume = aggrSQMtmp.imsi_bb_dl_somma_volume + (int64)eggsn1.cdrSchema.datavolumeFBCDownlink[i];
			}
		}
		for(int32 i in range(eggsn1.cdrSchema.datavolumeFBCUplink))
		{
			if (eggsn1.cdrSchema.datavolumeFBCUplink[i] != "0")
			{
				//appTrc(Trace.error,"incremento imsi_bb_ul_somma_volume");
				aggrSQMtmp.imsi_bb_ul_somma_volume = aggrSQMtmp.imsi_bb_ul_somma_volume + (int64)eggsn1.cdrSchema.datavolumeFBCUplink[i];
			}
		}
	} 
}

void addCdrImsi(AggregateSqmSchema eggsn, mutable SqmBroadImsiCapnet cdr, rstring key, rstring nameOutputFile, rstring filePrefix, rstring fileSuffix, float64 startROP, float64 endROP)
{
	mutable BroadImsiCapnet firstEntry = {};
	mutable BroadImsiCapnet existEntry = {};
	
	if(has(cdr, key) == false) 
    {   
    	firstEntry.nameOutputFile = nameOutputFile; 
		firstEntry.filePrefix = filePrefix;
		firstEntry.suffix = fileSuffix;									
										
    	firstEntry.ggsn_dn = eggsn.sito_capnet;
		firstEntry.start_time = getUtilConvert((rstring)startROP); 
		firstEntry.end_time = getUtilConvert((rstring)endROP);
		countCdrsIMSI(eggsn, firstEntry) ;
		insertM(cdr, key, firstEntry) ;
    }  
    else 
    {     
     	existEntry = cdr[key];
     	
     	countCdrsIMSI(eggsn, existEntry) ;
 					
 		removeM (cdr, key);
	    insertM( cdr, key, existEntry);
    }
}


BroadImsiCapnet initEmptyEndRopTupleImsiCapnet() 
{
	mutable BroadImsiCapnet result = {};
	
	result.endROP = true;
	
	return result;
}