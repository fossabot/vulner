namespace com.ti.oss.common.cdr.file;

use com.ti.oss.common.types::*;
use com.ibm.streamsx.file::*;
use com.ti.oss.common.event::*;
use com.ti.oss.common.cdr.util::*;

public composite FileExportCdrVolta(input stream<FileHeader h, FileTrailer t> IBigData
							  ;output OutEvent)
{
	type
		HeaderTrailer = FileHeader h, FileTrailer t, int32 count;
		
	graph
	
	   stream<FileHeader h, FileTrailer t> OJoin = Custom(IBigData)	//;IBigData 
	   {	 
	   		logic state :
	   		{
	   			mutable map<rstring, HeaderTrailer> InputFilesMap; 
	   		}                 
	
	   		
	   		onTuple IBigData : 
	   		{
	   			if(!has(InputFilesMap, IBigData.h.fName)) {
	   				mutable HeaderTrailer tmp = {};
	   				tmp = {h = IBigData.h, t = IBigData.t, count = 1};
	   				insertM(InputFilesMap, IBigData.h.fName, tmp);
	   			} 
	   			else {
	   				InputFilesMap[IBigData.h.fName].count++;
	   			}
	   			
	   			if(InputFilesMap[IBigData.h.fName].count == 4) {
	   				submit({h = InputFilesMap[IBigData.h.fName].h, t = InputFilesMap[IBigData.h.fName].t}, OJoin);
	   				removeM(InputFilesMap, IBigData.h.fName);
	   			}
	   		}         
	   			   		                     		
       }
        
        
        (stream<Event e> OutEvent) as MoveFile = Custom(OJoin)	
        {
           logic
           
             state :
             {
                mutable FileHeader fHeader = {};					
             }
           
             onTuple OJoin:
             {
                appTrc(Trace.trace, "FileExport - renaming file \"" + OJoin.t.outFile + "\" to \"" + OJoin.t.exportFile + "\"...");
                
                if(fHeader != OJoin.h) fHeader = OJoin.h;
                
             	mutable int32 error = 0;
             	
             	rstring pathname = getPathFile(OJoin.t.exportFile);
             	if(!exists(pathname)) {
	            	int32 error = createDirectory(pathname);
                    if(error!=0) {
                     appTrc(Trace.error, "FileExportCdr - Can not create directory \"" + pathname + 
                                         "\". Please, verify user rights - Error is \"" + strerror(error) + "\"");                     
                   }
           	    }
             	
             	if(!exists(OJoin.t.exportFile))
             	{
             	  error = rename(OJoin.t.outFile, OJoin.t.exportFile);  
             	}
             	
             	// TODO:
             	//   1. Compile with assert disable for final release
             	//   2. An alarm MUST be sent.
             	if(error!=0) 
             	{
             	   appTrc(Trace.error, "FileExport - Could not rename file \"" + OJoin.t.outFile + "\" into \"" + OJoin.t.exportFile + "\"" + strerror(error));
             	} 
             	else
             	{
             	   // Write event
             	   mutable Event event = {};
             	   event.eventType = 200;
                   event.resultCode = 0;
                   event.rawFile = filename(h.fName);
                   event.eventCorrelationId = h.eventCorrelationId;
                   event.rop = h.rop;
                   event.emId = h.emId;
                   event.neId = h.neId;
                  
                   event.eventText = "File exported : "+ OJoin.t.exportFile;
                   boolean eventEnabled = mkEvent( event, h.sessionBeginTime );
                   
                   if(eventEnabled) 
                   		submit ({e=event}, OutEvent);                                                     
             	} 
             }
             
             onPunct OJoin: 
             {
  				 if (currentPunct() == Sys.WindowMarker)
  				 {
					  // Prepare the event to submit 
					  mutable Event event = {};		
					  event.eventType = 210;
					  event.resultCode = 0;
					  event.rawFile = filename(fHeader.fName);
					  event.eventCorrelationId = fHeader.eventCorrelationId;
					  event.rop = fHeader.rop;
					  event.emId = fHeader.emId;
					  event.neId = fHeader.neId;
					  event.eventText = "Row file processing completed";
      				  boolean eventEnabled = mkEvent( event, fHeader.sessionBeginTime );
      				  if(eventEnabled) submit ({e=event}, OutEvent);
      
    				  submit(Sys.WindowMarker, OutEvent);	
   				}	
             }
        } 
}

